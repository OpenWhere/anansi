/*
 * Copyright (c) 2012-2013 Ray A. Conner
 *
 * Permission is hereby granted, free of charge, to any person obtaining a
 * copy of this software and associated documentation files (the
 * "Software"), to deal in the Software without restriction, including
 * without limitation the rights to use, copy, modify, merge, publish,
 * distribute, sublicense, and/or sell copies of the Software, and to
 * permit persons to whom the Software is furnished to do so, subject to
 * the following conditions:
 *
 * The above copyright notice and this permission notice shall be included
 * in all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS
 * OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
 * MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
 * IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
 * CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
 * TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
 * SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 */

apply plugin: 'java'
apply plugin: 'maven'

// Task: cobertura
//   builds test coverage reports
apply plugin: 'cobertura'

// with jar.dependsOn clause below, automatically adds about.txt to all jars
apply plugin: 'about'

// Task: duplicates
//   detects duplicate classes on the classpath
apply plugin: 'duplicates'

// Task: dependencyUpdates
//   shows which dependencies have a later version available
apply plugin: 'versions'

sourceCompatibility = 1.6
targetCompatibility = 1.6
group = 'com.github.rconner'

task wrapper( type: Wrapper ) {
    gradleVersion = '1.9'
}

repositories {
    mavenCentral()
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'net.saliman:gradle-cobertura-plugin:2.1.0'
        classpath 'com.github.goldin.plugins.gradle:about:0.3.1'
        classpath 'com.github.goldin.plugins.gradle:duplicates:0.3.1'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.4'
    }
}

compileJava {
    options.debug = true
    options.compilerArgs = [ '-Xlint:unchecked', '-Xlint:deprecation' ]
}

dependencies {
    compile 'com.google.guava:guava:15.0'
    testCompile 'junit:junit:4.+'
}


// configure about plugin

jar {
    dependsOn 'about'
}

about {
    includeSystem = true
    includeProperties = true
    includePaths = true
    includeDependencies = [ 'compile', 'runtime' ]
}


// Show stdout and stderr when running tests

test {
    testLogging.showStandardStreams = true
}


// configure cobertura plugin

cobertura {
    coverageSourceDirs = [ project.sourceSets.main.java.srcDirs ]
    coverageFormats = [ 'html', 'xml' ]
}


// customize the maven pom artifact

uploadArchives {
    repositories {
        mavenDeployer {
            pom.project {
                url 'https://github.com/rconner/anansi'
                description 'A Java library with traversal primitives and operations for graph-like structures.'
                inceptionYear '2003'
                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }
            }
        }
    }
}


// create source and javadoc jars and add them as artifacts

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
